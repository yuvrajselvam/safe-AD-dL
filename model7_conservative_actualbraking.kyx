/* Exported from KeYmaera X v4.9.1 */

Theorem "Safe AD dL/07: Model 7 - Conservative - Actual braking"
  Description "Safety controller with conservative decisions, actual braking, vCrit is zero".
  Title "Model 7 - Conservative - Actual braking".

Definitions       /* function symbols cannot change their value */
  Real aMinS;     /* maximum braking - safety controller */
  Real aMaxN;     /* maximum acceleration - nominal controller */
  Real aMinN;     /* maximum braking - nominal controller */
  Real T;         /* maximum sampling time */
End.
    
ProgramVariables  /* program variables may change their value over time */
  Real x;         /* position of the ego car */
  Real v;         /* velocity of the ego car */
  Real a;         /* acceleration of the ego car */
  Real xCrit;     /* critical position, xCrit */
  Real tau;       /* clock variable */
  Real aN;        /* nominal acceleration */
End.


Problem           /* differential dynamic logic formula */ 
    v^2<=2*aMinS*(xCrit-x) & v>=0 & aMinS>0 & aMaxN>0 & T>0 & aMinN>0 /* init */
 -> [
      {
        {
          xCrit:=*;?(v^2<=2*aMinS*(xCrit-x)); aN:=*; ?(-aMinN<=aN & aN<=aMaxN); /* env*/  
          /* ctrl */
          {
                {?((v+aMaxN*T)^2 <= 2*aMinS*(xCrit-x-(v*T)-(aMaxN*T*T*0.5))); a:=aN;} /* if (ok) */
            ++ 
                {?(!(2*aMinS*(xCrit-x) >= v^2+(aMaxN+aMinS)*(aMaxN*T^2+2*T*v))); a:=*; ?(-aMinS<=a & a<0 & ((2*a*(xCrit-x)) <= -v^2));} /*else safety controller brakes within a range*/
          }
        }  
        tau := 0; {x'=v, v'=a, tau'=1 & v>=0 & tau<=T} /* plant */
      }*@invariant(v^2<=2*aMinS*(xCrit-x)) /* loop invariant */
    ]  (x>=xCrit -> v=0)   /* guarantee */
End.

Tactic "Safe AD dL/07: Model 7 - Conservative - Actual braking: Proof"
auto
End.

End.
